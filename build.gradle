buildscript {
    dependencies {
        classpath 'javax.inject:javax.inject:1'
    }
}

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'org.shosancold'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url 'https://jitpack.io'
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}


dependencies {
    implementation("dev.jorel:commandapi-paper-shade:11.1.1-SNAPSHOT")
    implementation 'net.objecthunter:exp4j:0.4.8'
    compileOnly ('javax.inject:javax.inject:1')
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
}

shadowJar {
    relocate 'dev.jorel.commandapi', 'org.shosancold.datapacksplus.lib.commandapi'
}
def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}


processResources {
    def props = [veasion: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}


import javax.inject.Inject

abstract class AutoUpdateTask extends DefaultTask {

    private final ExecOperations execOps

    @Inject
    AutoUpdateTask(ExecOperations execOps) {
        this.execOps = execOps
    }

    @TaskAction
    void run() {
        execOps.exec {
            commandLine(
                    "./autoUpdate",
                    "-c", "config.json",
                    "-f", "build/libs/DatapacksPlus-${project.version}-all.jar"
            )
        }
    }
}

tasks.register("autoUpdate", AutoUpdateTask) {
    description = "Automatically upload the jar to the server panel."
}